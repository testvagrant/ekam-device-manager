plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group 'com.testvagrant.ekam'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // Apache
    implementation('org.apache.commons:commons-lang3:3.13.0')

    //Google
    implementation('com.google.guava:guava:30.1.1-jre')

    // Square
    implementation('com.squareup.retrofit2:retrofit:2.9.0')
    implementation('com.squareup.retrofit2:converter-gson:2.9.0')

    //lombok
    implementation('org.projectlombok:lombok:1.18.28')
    annotationProcessor('org.projectlombok:lombok:1.18.28')
    testImplementation('org.projectlombok:lombok:1.18.28')
    testAnnotationProcessor('org.projectlombok:lombok:1.18.28')

    // TestVagrant
    implementation('com.testvagrant.ekam:ekam-commons:1.0.3')
    implementation('com.testvagrant.ekam:ekam-api:1.0.1')


    //logger
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation('com.testvagrant.ekam:ekam-logger:1.0.0')

    testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.2')
    testRuntimeOnly('org.junit.jupiter:junit-jupiter-engine:5.7.2')
}

test {
    useJUnitPlatform()
}


task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'com.testvagrant.ekam'
            artifactId 'ekam-device-manager'
            version '1.0.3'

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv("sonatypeUsername")
                password System.getenv("sonatypePassword")
//                username sonatypeUsername
//                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Ekam Device Manager Project to discover and manage devices'
            name 'Ekam Device Manager'
            url 'https://github.com/testvagrant/ekam-device-manager'
            organization {
                name 'com.testvagrant.ekam'
                url 'https://github.com/testvagrant'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/testvagrant/ekam-device-manager/issues'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'https://github.com/testvagrant/ekam-device-manager/-/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/testvagrant/ekam-device-manager'
                connection 'https://github.com/testvagrant/ekam-device-manager.git'
                developerConnection 'git@github.com:testvagrant/ekam-device-manager.git'
            }
            developers {
                developer {
                    name 'Sudarshan GS'
                }
                developer {
                    name 'Krishnanand B'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
}


signing {
    sign publishing.publications
}